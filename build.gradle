plugins {
    id 'org.springframework.boot' version "${spring_boot_version}"
    id 'io.spring.dependency-management' version "${spring_boot_dependency_management}"
    id 'com.google.cloud.tools.jib' version "${jib_plugin_version}"
    id 'jacoco'
    id 'java'
}

group = 'org.oneworldaccuracy'
version = "${app_version}"
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    compile 'com.vladmihalcea:hibernate-types-52:2.3.4'
    compile "org.hibernate.validator:hibernate-validator"
    compile 'org.passay:passay:1.4.0'
    compile 'com.sendgrid:sendgrid-java:4.1.2'

    compileOnly 'org.projectlombok:lombok:1.18.12'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation "org.apache.commons:commons-lang3"
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'io.springfox:springfox-bean-validators:3.0.0'

    runtimeOnly 'org.postgresql:postgresql'

    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testImplementation "com.h2database:h2"
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

test {
    useJUnitPlatform()
}

// Configure JIB to build docker image of the application
jib {
    from {
        image = "openjdk:8-jre-alpine"
    }
    to {
        image = "${rootProject.name.toLowerCase()}"
        tags = ["${project.version}".toString(), "latest"]
    }
    container {
        entrypoint = ["./entrypoint.sh"]
        ports = ["8080"]
        environment = [
            SPRING_OUTPUT_ANSI_ENABLED: "ALWAYS",
            SPRING_PROFILES_ACTIVE    : project.getProperties().getOrDefault("springProfiles", "")
        ]
        creationTime = "USE_CURRENT_TIMESTAMP"
    }
    extraDirectories {
        paths = file("src/main/jib")
        permissions = ["/entrypoint.sh": "755"]
    }
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
    dependsOn test
}

defaultTasks "build"

task stage(dependsOn: ['bootJar'])
